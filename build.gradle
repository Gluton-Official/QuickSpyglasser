plugins {
	id 'fabric-loom' version '0.10-SNAPSHOT'
	id 'com.matthewprenger.cursegradle' version '1.4.0'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	maven { url "https://maven.terraformersmc.com" }
	maven { url "https://maven.shedaniel.me/" }
	maven {
		name = "Ladysnake Libs"
		url = "https://ladysnake.jfrog.io/artifactory/mods"
	}
	flatDir {
		dirs 'libs' // for trinkets since latest version isn't on maven
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"

	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	modImplementation "me.shedaniel.cloth:cloth-config-fabric:${project.cloth_version}", {
		exclude group: "net.fabricmc.fabric-api"
	}
	modImplementation "dev.emi:trinkets:${project.trinkets_version}", {
		exclude group: "net.fabricmc.fabric-api"
	}
	// required for trinkets
	modImplementation "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-base:${project.cca_version}"
	modImplementation "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-entity:${project.cca_version}"

	modCompileOnly "com.terraformersmc:modmenu:${project.modmenu_version}", {
		exclude group: "net.fabricmc.fabric-api"
	}
	modCompileOnly "dev.emi:trinkets:${project.trinkets_version}"
	// required for trinkets
	modCompileOnly "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-base:${project.cca_version}"
	modCompileOnly "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-entity:${project.cca_version}"

	modRuntimeOnly "com.terraformersmc:modmenu:${project.modmenu_version}"
	modRuntimeOnly "dev.emi:trinkets:${project.trinkets_version}"
	// required for trinkets
	modRuntimeOnly "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-base:${project.cca_version}"
	modRuntimeOnly "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-entity:${project.cca_version}"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 17
}

java {
	withSourcesJar()
}

curseforge {
	apiKey = findProperty('curseKey') ?: 0
	project {
		id = "${curse_id}"
		releaseType = "${curse_release}"
		changelogType = 'markdown'
		changelog = file('CHANGELOG.md')
		addGameVersion "Java 17"
		addGameVersion "Fabric"
		addGameVersion project.minecraft_version
		mainArtifact(file("${project.buildDir}/libs/${archivesBaseName}-${version}.jar"))

		relations {
			requiredDependency 'fabric-api'
			requiredDependency 'cloth-config2'
			optionalDependency 'modmenu'
			optionalDependency 'trinkets-fabric'
		}

		afterEvaluate {
			uploadTask.dependsOn(remapJar)
		}
	}
	options {
		debug = true
		forgeGradleIntegration = false
	}
}